#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long ();
use TAP::Harness;
use TAP::Harness::Archive;

Getopt::Long::Parser->new(
	config => [ qw/ pass_through / ],
)->getoptions(
	'jobs:1'    => \(my $jobs = $ENV{TEST_JOBS}),
	'archive=s' => \my $archive,
) or die "$0: Couldn't getoptions()";

TAP::Harness::Archive->new({
	jobs             => $jobs,
	archive          => $archive,
	($ENV{GIT_TEST_OPTS}
	 ? (test_args    => [ split /\s+/, $ENV{GIT_TEST_OPTS} ])
	 : ()),
	extra_properties => {},
})->runtests(munge_testfiles(@ARGV));

# To work around a regression in TAP::Harness between 3.17_01 and 3.22
sub munge_testfiles {
	require version;
	if (version->parse($TAP::Harness::VERSION) > version->parse("3.17") and
		version->parse($TAP::Harness::VERSION) < version->parse("3.22")) {
		# See https://rt.cpan.org/Public/Bug/Display.html?id=59186
		# and https://rt.cpan.org/Public/Bug/Display.html?id=59457
		return map { "./$_" } @_
	} else {
		return @_;
	}
}
